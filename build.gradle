apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'idea'

group = 'uk.org.tombolo'
version = '0.1.0'

repositories {
    maven { name = "osgeo"; url = "http://download.osgeo.org/webdav/geotools/" }
	maven { name = "osm4j"; url = "http://mvn.topobyte.de" }
    mavenCentral()
}

dependencies {
	// Geo Tools
	compile group:'org.geotools',			name: 'gt-referencing', 	version: '14.0'
	compile group:'org.geotools',			name: 'gt-geojson', 		version: '14.0'
	compile group:'org.geotools',			name: 'gt-shapefile', 		version: '14.0'
	compile group:'org.geotools',			name: 'gt-epsg-extension', 	version: '14.0'
	compile group:'org.geotools',			name: 'gt-epsg-hsql',		version: '14.0'
	compile group:'org.geotools.jdbc',		name: 'gt-jdbc-postgis',	version: '14.0'
	compile group:'org.geotools',			name: 'gt-geojsondatastore',	version: '15.0'

    // POI Excel file manipulation
	compile group: 'org.apache.poi',		name: 'poi',		version: '3.15'
	compile group: 'org.apache.poi',		name: 'poi-ooxml',	version: '3.15'
	
	// JSON handling
	compile group: 'javax.json', 			name: 'javax.json-api',			version: '1.0'
	compile group: 'com.googlecode.json-simple', name: 'json-simple', 		version: '1.1.1'
	compile group: 'com.google.code.gson',	name: 'gson', 					version: '2.4'
	compile group: 'com.github.fge', 		name: 'json-schema-validator',	version: '2.2.6'
	
	// Commons
	compile group: 'commons-io',			name: 'commons-io',				version: '2.4'
	compile group: 'org.apache.commons',	name: 'commons-compress',		version: '1.10'
	compile group: 'org.apache.commons',	name: 'commons-collections4',	version: '4.1'
	compile group: 'org.apache.commons',	name: 'commons-csv',			version: '1.2'
	compile group: 'org.apache.commons',	name: 'commons-math3',			version: '3.6.1'

	// ORM
	compile group: 'org.postgresql', 		name: 'postgresql', 			version: '9.4-1200-jdbc41'
	compile group: 'org.hibernate', 		name: 'hibernate-core',			version: '5.2.1.Final'
	compile group: 'org.hibernate',			name: 'hibernate-spatial',		version: '5.2.1.Final'
	compile group: 'org.hibernate',			name: 'hibernate-java8',		version: '5.2.1.Final'
	compile group: 'org.hibernate.javax.persistence',	name: 'hibernate-jpa-2.1-api',	version: '1.0.0.Final'

	// Testing
	testCompile group: 'org.skyscreamer',	name: 'jsonassert',			version: '1.3.0'
	testCompile group: 'org.hamcrest', 		name: 'hamcrest-core', 		version: '1.3'

	// Logging
	compile group: 'ch.qos.logback',		name: 'logback-classic',	version: '1.1.3'

	// Reflection
	compile group: 'org.reflections', 		name: 'reflections',		version: '0.9.10'

	// osm4j
	compile 'de.topobyte:osm4j-core:0.0.20'
	compile 'de.topobyte:osm4j-utils:0.0.23'
	compile 'de.topobyte:osm4j-xml:0.0.5'
	compile 'de.topobyte:osm4j-pbf:0.0.10'
	compile 'de.topobyte:osm4j-tbo:0.0.10'
	compile 'de.topobyte:osm4j-geometry:0.0.15'
	
}

configurations.all {
	exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

task copyDeps(type: Copy) {
	from configurations.runtime
	into 'build/dependency-cache/'
}

task runExport(dependsOn: ['classes'], type: JavaExec) {
	main = 'uk.org.tombolo.DataExportRunner'
	classpath = sourceSets.main.runtimeClasspath
	jvmArgs '-disableassertions'

	doFirst {
		def configured = project.hasProperty('databaseURI') && project.hasProperty('databaseUsername') && project.hasProperty('databasePassword');
		if (!configured) throw new GradleException('Environment not configured. See the README.')

		systemProperty("databaseURI", databaseURI)
		systemProperty("databaseUsername", databaseUsername)
		systemProperty("databasePassword", databasePassword)
		systemProperty("environment", "export")

		def argumentsSet = project.hasProperty('dataExportSpecFile') && project.hasProperty('outputFile');
		if (!argumentsSet) throw new GradleException('Required properties not specified. See the README.')
		if (!project.hasProperty('clearDatabaseCache')) { ext.clearDatabaseCache = false }
		if (!project.hasProperty('forceImports')) { ext.forceImports = "" }

		args(dataExportSpecFile, outputFile, forceImports, clearDatabaseCache)
	}
}

task runCorrelation(dependsOn: ['classes'], type: JavaExec) {
	main = 'uk.org.tombolo.CorrelationAnalysisRunner'
	classpath = sourceSets.main.runtimeClasspath
	jvmArgs '-disableassertions'

	doFirst {
		def configured = project.hasProperty('databaseURI') && project.hasProperty('databaseUsername') && project.hasProperty('databasePassword');
		if (!configured) throw new GradleException('Environment not configured. See the README.')

		systemProperty("databaseURI", databaseURI)
		systemProperty("databaseUsername", databaseUsername)
		systemProperty("databasePassword", databasePassword)
		systemProperty("environment", "export")

		def argumentsSet = project.hasProperty('dataExportSpecificationPath') && project.hasProperty('dataExportOutputPath') && project.hasProperty('correlationAnalysisOutputPath');
		if (!argumentsSet) throw new GradleException('Required properties not specified. See the README.')
		if (!project.hasProperty('clearDatabaseCache')) { ext.clearDatabaseCache = false }
		if (!project.hasProperty('forceImports')) { ext.forceImports = "" }

		args(dataExportSpecificationPath, dataExportOutputPath, correlationAnalysisOutputPath, forceImports, clearDatabaseCache)
	}
}

task runCatalogue(dependsOn: ['classes'], type: JavaExec) {
	main = 'uk.org.tombolo.DataCatalogueRunner'
	classpath = sourceSets.main.runtimeClasspath
	jvmArgs '-disableassertions'

	doFirst {
		def argumentsSet = project.hasProperty('importerClassName');
		if (!argumentsSet) throw new GradleException('Required properties not specified. See the README.')
		if (!project.hasProperty('datasetId')) { ext.datasetId = "" }

		args(importerClassName, datasetId)
	}
}

task exportCatalogue(dependsOn: ['classes'], type: JavaExec) {
	main = 'uk.org.tombolo.CatalogueExportRunner'
	classpath = sourceSets.main.runtimeClasspath
	jvmArgs '-disableassertions'

	doFirst {
		def argumentsSet = project.hasProperty('outputFile');
		if (!argumentsSet) throw new GradleException('Required properties not specified. See the README.')

		args(outputFile)
	}
}

test {
	maxHeapSize = "1024m"
	jvmArgs '-enableassertions', '-disableassertions:org.geotools...'

	doFirst {
		def configured = project.hasProperty('testDatabaseURI') && project.hasProperty('testDatabaseUsername') && project.hasProperty('testDatabasePassword');
		if (!configured) throw new GradleException('Test environment not configured. See the README.')

		systemProperty("databaseURI", testDatabaseURI)
		systemProperty("databaseUsername", testDatabaseUsername)
		systemProperty("databasePassword", testDatabasePassword)
		systemProperty("environment", "test")
	}
}
